// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:

    Not(in=zx, out=notZx);
    And16(a=x,
          b[0]=notZx,
          b[1]=notZx,
          b[2]=notZx,
          b[3]=notZx,
          b[4]=notZx,
          b[5]=notZx,
          b[6]=notZx,
          b[7]=notZx, 
          b[8]=notZx, 
          b[9]=notZx,
          b[10]=notZx,
          b[11]=notZx,
          b[12]=notZx,
          b[13]=notZx,
          b[14]=notZx,
          b[15]=notZx,
          out=xAfterZx);

    Not16(in=xAfterZx, out=notX);
    Mux16(a=xAfterZx, b=notX, sel=nx, out=xAfterPreProcessing);



    Not(in=zy, out=notZy);
    And16(a=y,
          b[0]=notZy,
          b[1]=notZy,
          b[2]=notZy,
          b[3]=notZy,
          b[4]=notZy,
          b[5]=notZy,
          b[6]=notZy,
          b[7]=notZy, 
          b[8]=notZy, 
          b[9]=notZy,
          b[10]=notZy,
          b[11]=notZy,
          b[12]=notZy,
          b[13]=notZy,
          b[14]=notZy,
          b[15]=notZy,
          out=yAfterZy);

    Not16(in=yAfterZy, out=notY);
    Mux16(a=yAfterZy, b=notY, sel=ny, out=yAfterPreProcessing);



    Add16(a=xAfterPreProcessing, b=yAfterPreProcessing, out=sumResult);
    And16(a=xAfterPreProcessing, b=yAfterPreProcessing, out=bitwiseResult);
    Mux16(a=bitwiseResult, b=sumResult, sel=f, out=result);


    Not16(in=result, out=notResult);
    Mux16(a=result, b=notResult, sel=no, out[15]=outputSignBit, out[15]=ng, out=finalResult, out=out);


    Add16(a=finalResult, b=true, out[15]=outputMinus1SignBit, out=finalResultMinus1);
    Not(in=outputSignBit, out=notOutputSignBit);
    And(a=notOutputSignBit, b=outputMinus1SignBit, out=zr);
}







